#!/Users/brian/bin/perl
use v5.26;
use utf8;
use open qw(:std :utf8);
use experimental qw(signatures);

use FindBin;
use lib "$FindBin::Bin/../lib";

use Local::CPANSA;

use Mojo::DOM;
use Mojo::URL;
use Mojo::UserAgent;
use Mojo::Util qw(dumper);

=head1 NAME

make_feed - turn the CVE search results into RSS

=head1 SYNOPSIS

	% perl util/make_feed

=head1 DESCRIPTION

B<make_feed> grabs some search results and turns those into items in
an RSS feed. This way, interested people can track new results that
we should add to the database.

If there are no new results for a day, this adds an item in the RSS
to note that there was nothing new. That we, we notice that it's still
running.

=cut

my $feed = start_xml();

my $no_reports = 1;


foreach my $v ( get_unevaluated_cve()->@* ) {
	add_item( $feed, $hash );
	}

add_default_item( $feed ) if $no_reports;

say '<?xml version="1.0" encoding="UTF-8" standalone="no" ?>' . $feed;

sub add_default_item ( $feed ) {
	my $message = "No new Perl CVEs for " . localtime;
	my $entry = tag('entry')->at('entry');
	$entry->append_content( tag( id      => "$$-" . time ) );
	$entry->append_content( tag( title   => $message ) );
	$entry->append_content( tag( content => $message ) );

	$feed->append_content($entry);
	}

sub add_item ( $feed, $hash ) {
	my $entry = tag('entry')->at('entry');
	$entry->append_content( tag( id      => $hash->{cve}         ) );
	$entry->append_content( tag( title   => $hash->{cve}         ) );
	$entry->append_content( tag( content => $hash->{description} ) );
	$entry->append_content( tag( 'link' ) );

	$entry->at('link')->attr( rel => 'alternate', href => $hash->{url} );

	$feed->append_content($entry);
	}

sub get_json () {
	my $url = "https://services.nvd.nist.gov/rest/json/cves/2.0?keywordSearch=Perl";
	my $json = Mojo::UserAgent
		->new->get($url)
		->res
		->json;
	}

sub now { # 2003-12-13T18:30:02Z
	my @gmtime = gmtime();
	$gmtime[5] += 1900;
	$gmtime[4] += 1;

	my $now = sprintf '%4d-%02d-%02dT%02d:%02d:%02dZ', @gmtime[5,4,3,2,1,0];
	}

sub start_xml () {
	my $feed = Mojo::DOM->new_tag('feed', xmlns => 'http://www.w3.org/2005/Atom');

	my $description = <<~"HERE";
		Common Vulnerabilities and Exposures related to Perl and CPAN.

		This is part of the CPAN Audit project. See
		https://github.com/briandfoy/cpan-security-advisory .
		HERE

	my %hash = (
		id          => 'https://www.theperlreview.com/perl-cve-atom.xml',
		title       => 'Perl and CPAN CVEs',
		subtitle    => $description,
		updated     => now(),
		);

	foreach my $tag ( keys %hash ) {
		$feed->at('feed')->append_content( tag($tag, $hash{$tag}) );
		}

	$feed->at('feed')->append_content( Mojo::DOM->new_tag( 'link', rel => 'self', href => $hash{id}, '' ) );
	$feed->at('feed');
	}

sub tag ( $tag, $content = undef ) { Mojo::DOM->new_tag($tag, $content // '' ) }
