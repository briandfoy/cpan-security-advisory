#!/usr/bin/env perl
use v5.26;
use experimental qw(signatures);

use strict;
use warnings;

use FindBin qw($Bin);
use lib ("$Bin/../lib");

use Antsy qw(:all);
use Carp qw(croak);
use CPAN::DistnameInfo;
use Cwd qw(getcwd);
use File::Basename qw(basename dirname);
use File::Spec::Functions qw(catfile);
use HTTP::Tiny;
use JSON ();
use MetaCPAN::Client;
use Module::CoreList;
use Mojo::File;
use Mojo::JSON qw(decode_json encode_json);
use PerlIO::gzip;
use Ref::Util qw(is_plain_arrayref);

use YAML::Tiny;
use File::Temp;

use subs qw(config message);

=head1 NAME

util/generate - create the data for lib/CPANSA/DB.pm

=head1 SYNOPSIS

	# usual operation, outputs to lib/CPANSA/DB.pm
	# gets data from .../cpansa/*.yml
	% perl util/generate

	# same thing
	% perl util/generate --perl-module

	# usual operation, outputs to lib/CPANSA/DB.pm
	# gets data from other_source/*.yml
	% perl util/generate other_source/*.yml

	# suppress progress messages
	% perl util/generate -q
	% perl util/generate --quiet

	# output JSON instead of a Perl module
	% perl util/generate --json

	# output both
	% perl util/generate --json --perl-module

=head1 DESCRIPTION

This program chews through the CPAN security advisory reports and
makes the L<CPANSA::DB> module. This is the same thing that
L<CPAN::Audit::DB> used to be when it was in the L<CPAN::Audit>
distribution.

=head1 AUTHOR

Original author: Viacheslav Tykhanovskyi (C<vti@cpan.org>)

Maintained by: brian d foy (C<briandfoy@pobox.com>)

=head1 LICENSE

L<CPANSA::DB> is dual-licensed under the GPL or the Artistic License.
See the included F<LICENSE> file for details.

=cut

$|++;

run(@ARGV) unless caller;

sub _message ( $fh, $message, $indent = 0 ) {
	state $p = ' ' x 2;
	$message =~ s/\v+\z//;
	say {$fh} $p x $indent, $message
}

sub add_meta ( $db ) {
	my $commit = get_commit();
	my $repo = get_repo();

	$db->{meta} = {
		repo      => $repo,
		generator => $0,
		date      => scalar gmtime,
		epoch     => time,
		commit    => $commit,
	};
}

sub debug ( $message, $indent = 0 ) {
	return unless config()->{debug};
	_message *STDERR,
		text_cyan() . $message . reset(),
		$indent;
}

sub info ( $message, $indent = 0 ) {
	return if config()->{quiet};
	_message *STDOUT,
		$message,
		$indent;
}

sub unexpected ( $message, $indent = 0 ) {
	_message *STDERR,
		text_bright_red() . $message . reset(),
		$indent;
}

sub dumper {
	state $rc = require Data::Dumper;
	Data::Dumper->new([@_])->Indent(1)->Sortkeys(1)->Terse(1)->Useqq(1)->Dump
}

sub add_corelist_versions ($db) {
	info "Adding modules from corelist";
	my $indent = 1;

	my $mcpan = MetaCPAN::Client->new;

	my %Dists;
	my %Versions_inverted;
	my $MainModule;
	my $MetaCPAN_calls = 0;

	my $main_module_file = 'main-modules.json';
	if( -e $main_module_file ) {
		info "restoring MainModule", $indent;
		$MainModule = decode_json( Mojo::File->new($main_module_file)->slurp );
		}

	# Get all namespaces from all corelist entries
	# Get version history and perl release for each
	foreach my $perl_version ( sort { $a <=> $b } keys %Module::CoreList::version ) {
		info "Perl $perl_version", $indent;
		$indent++;
		my $v_hash = $Module::CoreList::version{$perl_version};
		foreach my $module ( sort keys $v_hash->%* ) {
			info "Checking module $module", $indent;
			$indent++;
			unless( exists $db->{module2dist}{$module} ) {
				$indent--;
				next;
				}
			my $dist = $db->{module2dist}{$module};
			info "Dist for $module is $dist", $indent;

			if( $dist eq 'perl' ) {
				$indent--;
				next;
				}

			my $module_version = $v_hash->{$module};

			# We need the main module so we can get its version, which
			# will be the dist version
			unless( exists $MainModule->{modules}{$module} ) {
				info "Looking up release for $module", $indent;
				$MetaCPAN_calls++;
				$MainModule->{modules}{$module} = eval { $mcpan->release($dist)->main_module } // '<undef>';
				}

			my $dist_version = $v_hash->{ $MainModule->{modules}{$module} };
			# printf STDERR "    %-25s %-25s %-25s %6f\n", $module, $dist, $MainModule->{$module}, $dist_version // '<undef>';

			unless( exists $Versions_inverted{$dist} ) {
				foreach my $tuple ( $db->{dists}{$dist}{versions}->@* ) {
					$Versions_inverted{$dist}{$tuple->{'version'}}++;
					}
				}

			# skip versions already in the list in $db
			no warnings 'uninitialized';

			if( exists $Versions_inverted{$dist}{$dist_version} ) {
				$indent--;
				next;
				}

			$Versions_inverted{$dist}{$dist_version}++;

			info "Adding $dist $dist_version", $indent;

			die "dists key does not exist" unless exists $db->{dists};
			unless( exists $db->{dists}{$dist} ) {
				unexpected "dists does not have a <$dist> key (but now it does)", $indent;
				$db->{dists}{$dist} = {
					advisories  => [],
					main_module => $MainModule->{modules}{$module},
					versions    => [],
					};
				}

			unless( exists $db->{dists}{$dist}{'versions'} ) {
				unexpected "<$dist> does not have a versions key (but now it does)", $indent;
				$db->{dists}{$dist}{'versions'} = [];
				}

			unexpected "dists <$dist> 'versions' value is not an array ref"
				unless is_plain_arrayref $db->{dists}{$dist}{'versions'};

			push $db->{dists}{$dist}{'versions'}->@*, {
				dual_lived   => 1,
				perl_release => $perl_version,
				date         => $Module::CoreList::released{$perl_version} . 'T00:00:00',
				version      => $dist_version,
				};

			$indent--;
			}

		$indent--;
		}

	info "saving MainModule", $indent;
	Mojo::File->new($main_module_file)->spew(
		encode_json($MainModule)
		);

	return \%Dists;
}

sub all_releases {
	my ($distribution) = @_;
	my $tries = 0;

	my $content_json;
	FETCH: {
		sleep $tries * 10;
		debug "Try $tries for $distribution";
		my $response = HTTP::Tiny->new->get(
			'http://fastapi.metacpan.org/v1/release/versions/'.$distribution
		);

		$content_json = eval { JSON::decode_json( $response->{content} ) };
		last if $content_json;
		redo FETCH unless $tries++ > 3;
		}

	my @results = sort { $a->{date} cmp $b->{date} } @{ $content_json->{releases} };
	return unless @results;

	return @results;
}

sub default_version ($output_file) {
	my $opts = config();
	my( $year, $month, $day ) = (localtime)[5,4,3];

	my $date = sprintf '%4d%02d%02d', $year + 1900, $month + 1, $day;
	my( $previous_date, $previous_serial ) = get_previous_date_serial( $output_file );
	debug "PREVIOUS DATE: $previous_date PREVIOUS SERIAL: $previous_serial";

	my $serial = sprintf '%03d', $previous_date == $date ? $previous_serial + 1 : 1;
	debug "NEW SERIAL: $serial";
	debug "NEW DATE: $date";

	my $version = join '.', $date, $serial;
	debug "NEW VERSION: $version";
	return $version;
}

sub get_commit {
	my( $commit ) = `git rev-parse HEAD`;
	chomp ($commit);
	$commit;
}

sub get_file_list ( $args ) {
	unless( @$args and -e 'cpansa' ) {
		debug 'No arguments given for source directory: looking in cpansa/';
		@$args = glob( '{cpansa,generated_reports}/*.yml' );
	}

	my @files = ($^O eq 'MSWin32') ? map { glob } @$args : @$args;

	\@files;
}

sub get_previous_date_serial ( $file ) {
	open my $fh, '<:encoding(UTF-8)', $file or return (-1, 0);
	while( <$fh> ) {
		next unless /VERSION\s*=\s*'(\d{8})\.(\d{3})'/;
		return ( $1, $2 );
	}
	return;
}

sub get_repo {
	my $repo = `git config --get remote.origin.url`;
	chomp($repo);
	$repo =~ s|\Agit\@github.com:|https://github.com/|;
	$repo;
}

sub json_file () {
	state $file = 'cpan-security-advisory.json';
	$file;
}

sub output_gpg_signature ( $string, $file ) {
	debug "Outputting GPG sig for <$file>";
	debug "PWD: " . getcwd();

	my $opts = config();

	return unless defined $opts->{gpg_key};
	debug "Still going with GPG sig for <$file>";

	my $gpg_file = $file . '.gpg';

	my $dir = dirname($gpg_file);
	debug "DIR $dir exists: " . !! -e $dir;

	state $rc = require Encode;
	my $octets = Encode::encode("UTF-8", $string);

	my @command = (
		'gpg', '--batch', '--yes', '-o', $gpg_file, '-sb',
		'--armor', '-u', $opts->{gpg_key}
		);
	debug "COMMAND is @command";

	open my $gpg_fh, '|-', @command;

	my $success = print { $gpg_fh } $octets;
	debug "SUCCESS $success";

	close $gpg_fh or croak "Problem making GPG signature: $!";
	if( $? ) {
		croak "Problem making GPG signature: $?";
	}

	return 1;
}

sub packages_provides ($db) {
	info "extracting 02packages data";
	my $indent = 1;
	my $ua = HTTP::Tiny->new;

	my $tmpdir = File::Temp::tempdir();
	my $details_file = catfile($tmpdir, '02packages.details.txt.gz');
	info "Downloading 02packages.details.txt.gz (this may take awhile)", $indent;
	$ua->mirror( 'http://www.cpan.org/modules/02packages.details.txt.gz',
		$details_file );
	info "Downloaded 02packages.details.txt.gz", $indent;
	info "Digesting 02packages.details.txt.gz (this may take awhile)", $indent;

	open my $fh, '<:gzip', $details_file
	  or die "Can't open '$details_file': $!";

	# skip over the header. Read until there's a black line
	while ( defined( my $line = <$fh> ) ) {
		chomp $line;
		last if $line eq '';
	}

	while ( defined( my $line = <$fh> ) ) {
		my ( $module, $version, $pathname ) = split /\s+/, $line;
		next unless $module && $pathname;

		my $dist_info = CPAN::DistnameInfo->new($pathname);
		next unless $dist_info;

		my $author = $dist_info->cpanid;
		my $dist   = $dist_info->dist;
		my $name   = $dist_info->distvname;

		next unless $dist;

		next unless $db->{dists}->{$dist};

		$db->{module2dist}->{$module} = $dist;
	}

	close $fh;
}

sub perl_module_file ($package) {
	my @parts = split /::/, $package;
	$parts[-1] .= '.pm';

	catfile('lib', @parts);
}

sub process_files ( $files ) {
	my $indent = 1;
	info "Processing files", $indent;

	my %db;
	foreach my $file ( $files->@* ) {
		$indent++;
		info "Reading $file", $indent;
		my $yaml = YAML::Tiny->read($file)->[0];
		debug sprintf( "Found %d reports", scalar $yaml->{advisories}->@* ), $indent;
		my %dists_in_file;
		my $n = 0;

		my $message = do {
			my $key = 'distribution';
			   if( ! exists  $yaml->{$key} ) { "Missing $key key in $file\n"    }
			elsif( ! defined $yaml->{$key} ) { "Undefined $key key in $file\n"  }
			elsif( ! length  $yaml->{$key} ) { "Empty $key key in $file\n"      }
			else { undef }
			};
		unexpected $message, $indent if defined $message;

		my $dist = $yaml->{distribution};

		if( exists $db{dists}{$dist}{advisories} ) {
			unexpected "Already have advisories for $dist", $indent;
		}
		else {
			$db{dists}{$dist}{advisories} = [];
		}

		foreach my $advisory ( $yaml->{advisories}->@* ) {
			$advisory->{distribution} = $dist;
			no warnings 'uninitialized';
			$advisory->{darkpan} = 'true' if( exists $yaml->{darkpan} && $yaml->{darkpan} eq 'true' );

			delete $advisory->{github_security_advisory};
			push $db{dists}{$dist}{advisories}->@*, $advisory;
		}
		$indent--;
	}

	packages_provides( \%db );

	foreach my $dist ( sort keys $db{dists}->%* ) {
		info "Processing $dist", $indent;
		$indent++;

		my @releases = all_releases($dist);
		if (!@releases) {
			unexpected "no releases found on CPAN for '$dist'", $indent;
			$db{dists}{$dist}{versions} = [];
			$db{dists}{$dist}{main_module} = '';
			$indent--;
			next;
		}

		# try to fetch the latest release, according to MetaCPAN.
		# if there is no 'latest' tag, grab the last item (because
		# the list is ordered by date).
		my ($main_module, @versions);
		foreach my $release (@releases) {
			push @versions, { date => $release->{date}, version => $release->{version} };
			if ($release->{status} eq 'latest') {
				$main_module = $release->{main_module};
			}
		}
		$main_module = $releases[-1]->{main_module} unless $main_module;

		$db{dists}{$dist}{versions} = \@versions;
		$db{dists}{$dist}{main_module} = $main_module;

		$indent--;
	}

	# XXX: need to investigate why this shows up as utf8
	$db{dists}{'perl'}{main_module} = 'perl';

	\%db;
}

sub process_options ( @args ) {
	state $rc = require Getopt::Long;

	my %results = (
		debug       => $ENV{CPAN_AUDIT_DEBUG} // 0,
		gpg_key     => $ENV{CPAN_AUDIT_GPG_KEY_FINGERPRINT},
		);
	*config = sub () { \%results };

	my %opts = (
		'debug|d'          => \ $results{debug},
		'dry-run|n'        => \ $results{dry_run},
		'gpg-key|g=s'      => \ $results{gpg_key},
		'module-version'   => \ $results{version},
		'quiet|q'          => \ $results{quiet},
	);

	my $p = Getopt::Long::Parser->new();
	$p->configure( qw(no_ignore_case) );
	$p->getoptionsfromarray( \@args, %opts );

	debug "Options: " . dumper( \%results );

	$results{version} //= default_version( perl_module_file('CPANSA::DB') );

	\@args;
}

sub run ( @args ) {
	my( $leftover_args ) = process_options( @args );
	my $opts = config();

	*message = $opts->{quiet} ? sub ($m) {} : sub ($m) { _message *STDOUT, $m };

	my $files = get_file_list( $leftover_args );
	die "Usage: <files>\n" unless @$files;

	exit if $opts->{dry_run};

	my $db = process_files( $files );
	add_meta( $db );

	add_corelist_versions( $db );

	my @packages = qw(CPAN::Audit::DB CPANSA::DB);

	PERL_MODULE: foreach my $package  ( @packages ) {
		say "Creating Perl module $package ...";
		my $string = stringify_data($db, $package);
		my $filename = perl_module_file($package);
		debug "Outputting perl module <$filename>";

		{
		open my $fh, '>:encoding(UTF-8)', $filename;
		print {$fh} $string;
		close $fh;
		output_gpg_signature( $string, $filename );
		}

		{
		my $pod = $string =~ s/^package.*//rms;
		my $pod_file = $filename =~ s/\.pm\z/\.pod/r;
		open my $fh, '>:encoding(UTF-8)', $pod_file;
		print {$fh} $pod;
		close $fh;
		}

		}

	JSON: {
		my $filename = json_file();
		say "Creating JSON $filename...";
		my $string = JSON::encode_json($db);
		debug "Outputting JSON file <$filename>";
		open my $fh, '>:encoding(UTF-8)', $filename;
		print {$fh} $string;
		close $fh;
		output_gpg_signature( $string, $filename );
	}
}

sub stringify_data ( $db, $package ) {
	my $opts = config();

	state $rc = require Data::Dumper;

	no warnings 'once';
	local $Data::Dumper::Sortkeys = 1;
	my $dump = Data::Dumper::Dumper( $db );
	$dump =~ s{^\$VAR1\s*=\s*}{};
	$dump =~ s{}{};

	my $commit = get_commit();
	my $repo = get_repo();

	my $string = <<~"EOF";
		# created by $0 at @{[ scalar gmtime ]}
		# $repo $commit

		=encoding utf8

		=head1 NAME

		$package - the CPAN Security Advisory data as a Perl data structure, mostly for CPAN::Audit

		=head1 SYNOPSIS

		This module is primarily used by L<CPAN::Audit>.

			use $package;

			my \$db = $package->db;

		=head1 DESCRIPTION

		The C<db> subroutine returns the CPAN Security Advisory (CPANSA) reports
		as a Perl data structure. However, anything can use this.

		Each release also comes with a F<.gpg> file that has the signature
		for the file. If you cannot confirm that the module file has the
		right signature, it might have been corrupted or modified.

		This module is available outside of CPAN as a release on GitHub:
		L<https://github.com/briandfoy/cpan-security-advisory/releases>.
		Each release on GitHub includes an attestation.

		There is also a JSON file that provides the same datastructure.

		=head2 Subroutines

		There is exactly one subroutine:

		=over 4

		=item * db

		Returns the hashref of all the CPANSA reports.

		=back

		=head1 VERIFYING

		This distribution now uses
		L<GitHub Attestations|https://github.blog/2024-05-02-introducing-artifact-attestations-now-in-public-beta/>,
		which allow you to verify that the archive file you have was made from
		the official repo.

		You need a GitHub account and the L<gh tool|https://github.com/larsks/ghcli>.

			# download the distro file from GitHub, MetaCPAN, or a CPAN mirror
			\$ gh auth login
			...follow instructions...
			\$ gh attestation verify CPANSA-DB-20241111.tar.gz --owner briandfoy

		Additionally, each release codes with GPG signature that allows you to
		verify that this. The key is the same one used when the database was
		distributed with L<CPAN::Audit>:

			\$ gpg --verify lib/CPANSA/DB.pm.gpg lib/CPANSA/DB.pm
			gpg: Signature made Mon Nov 18 11:00:10 2024 EST
			gpg:                using RSA key 75AAB42CBA0D7F37F0D6886DF83F8D5E878B6041
			gpg: Good signature from "CPAN::Audit (brian d foy) (https://github.com/briandfoy/cpan-audit) <bdfoy\@cpan.org>" [ultimate]

		=head1 SEE ALSO

		Everything is managed in GitHub:

		=over 4

		=item * L<https://github.com/briandfoy/cpan-security-advisory/releases>

		=back

		=cut

		package $package;

		use strict;
		use warnings;

		our \$VERSION = '$opts->{version}';

		sub db {
			$dump
		}

		__PACKAGE__;
		EOF
}
